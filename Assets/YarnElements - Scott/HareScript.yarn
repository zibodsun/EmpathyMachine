title: awake
tags:
---
<<declare $markersDone = false>>
<<declare $gunDone = false>>
<<declare $broomDone = false>>
<<declare $trainDone = false>>

<<declare $introDone = false>>
===

title: surprise
tags:
---
<<setActivity Hare introduction>>
<<jump introduction>>
===

title: introduction
tags:
---
Boo! #line:boo
About time sleepyhead, adventure awaits! #line:sleepyhead
-> A talking rabbit? #line:021fae0 
    I'm a HARE actually! #line:hareactually 
-> Adventure? #line:0587a70 
    Of course! #line:ofcourse 

We have many tasks and quests to complete! #line:tasksandquests 
How should we begin? #line:begin
<<jump chooseTask>>
===

title: chooseTask
tags:
---
<<if $trainDone && $broomDone && $gunDone && $markersDone>>
<<jump ending>>
<<endif>>

<<if $introDone>>
What should we do next? #line:whatnext 
<<endif>>

<<set $introDone to true>>

-> Put away markers <<if $markersDone is false>> #line:0624c59 
    Excellent idea! Follow me! #line:excellent 
    <<activateWondrousProperties Marker>>
    <<activateWondrousProperties Marker1>>
    <<activateWondrousProperties Marker2>>
    <<setActivity Hare markerPickup>>
    <<set $markersDone to true>>
-> Put away toy gun <<if $gunDone is false>> #line:05d9e33 
    Let's do it! #line:doit
    <<activateWondrousProperties Toygun>>
    <<setActivity Hare toygun>>
    <<set $gunDone to true>>
-> Put away broom <<if $broomDone is false>> #line:063e18d 
    Great choice. Come on! #line:greatchoice 
    <<activateWondrousProperties broom>>
    <<setActivity Hare broom>>
    <<set $broomDone to true>>
-> Put away train <<if $trainDone is false>> #line:02dfc93 
    If you say so! #line:sayso
    <<activateWondrousProperties Train>>
    <<setActivity Hare train>>
    <<set $trainDone to true>>
===

title: markers
tags:
---
Markers have been left all over the floor! #line:markersleft 
Let's put them back! #line:putback 
<<setActivity Hare drawing>>
===

title: draw
tags:
---
We have time for some fun first #line:funfirst 
Do some drawing! Then put them away when you're ready #line:somedrawing
<<startDrawing Paper>>
<<activate Hare DrawingCanvas>>
===

title: broom
tags:
---
The broom belongs in the corner. Let's return it #line:broomcorner
===

title: toygun
tags:
---
The toy gun has been left out. Put it back in the toy box #line:toygun 
===

title: train
tags:
---
Another toy left out. It belongs in the toy box #line:traintoy 
===

title: ending
tags:
---
Phew! I think we're done! #line:phew 
You put everything away and the room looks tidy. Well done! #line:welldone 
Give us some feedback on your experience before you go #line:feedback
I'm going to take a nap! #line:nap
<<feedback Hare>>
<<setActivity Hare end>>
===

title: sleeping
tags:
---
ZZZ #line:09d7063
===